// 7-segments digit display
//    a
//   ---
// f| g |b
//   ---
// e|   |c
//   ---
//    d
CHIP DigitDisplay {
    IN in[4];
    OUT a, b, c, d, e, f, g;
    PARTS:
    //   | 0 1 2 3 4 5 6 7 8 9 A b C d E F
    // --|--------------------------------
    // a | 1 0 1 1 0 1 1 1 1 1 1 0 1 0 1 1
    // b | 1 1 1 1 1 0 0 1 1 1 1 0 0 1 0 0
    // c | 1 1 0 1 1 1 1 1 1 1 1 1 0 1 0 0
    // d | 1 0 1 1 0 1 1 0 1 1 0 1 1 1 1 0
    // e | 1 0 1 0 0 0 1 0 1 0 1 1 1 1 1 1
    // f | 1 0 0 0 1 1 1 0 1 1 1 1 1 0 1 1
    // g | 0 0 1 1 1 1 1 0 1 1 1 1 0 1 1 1

    Not (in = in[0], out = n0);
    Not (in = in[1], out = n1);
    Not (in = in[2], out = n2);
    Not (in = in[3], out = n3);

    //---------------------------

	Nand(a=n3, b=n2, out=x0ch1);
	Nand(a=n1, b=n0, out=x0ch2);
	Or(a=x0ch1, b=x0ch2, out=ch0);

	Nand(a=n1, b=in[0], out=x1ch2);
	Or(a=x0ch1, b=x1ch2, out=ch1);

    Nand(a=in[1], b=n0, out=x2ch2);
	Or(a=x0ch1, b=x2ch2, out=ch2);

	Nand(a=in[1], b=in[0], out=x3ch2);
	Or(a=x0ch1, b=x3ch2, out=ch3);

	Nand(a=n3, b=in[2], out=x4ch1);
	Or(a=x4ch1, b=x0ch2, out=ch4);
	Or(a=x4ch1, b=x1ch2, out=ch5);
	Or(a=x4ch1, b=x2ch2, out=ch6);
	Or(a=x4ch1, b=x3ch2, out=ch7);

	Nand(a=in[3], b=n2, out=x8ch1);
	Or(a=x8ch1, b=x1ch2, out=ch9);
	Or(a=x8ch1, b=x2ch2, out=cha);
	Or(a=x8ch1, b=x3ch2, out=chb);

	Nand(a=in[3], b=in[2], out=x12ch1);
	Or(a=x12ch1, b=x0ch2, out=chc);
	Or(a=x12ch1, b=x1ch2, out=chd);
	Or(a=x12ch1, b=x2ch2, out=che);
	Or(a=x12ch1, b=x3ch2, out=chf);

    //---------------------------
    
    And(a=ch1, b=ch4, out=a1);
    And(a=chb, b=chd, out=a2);
	And(a=a1, b=a2, out=a);

    And(a=ch5, b=ch6, out=b1);
    And(a=chb, b=chc, out=b2);
    And(a=che, b=chf, out=b3);
    And(a=b1, b=b2, out=b4);
	And(a=b4, b=b3, out=b);

    And(a=ch2, b=chc, out=c1);
    And(a=c1, b=b3, out=c);

    And(a=ch1, b=ch7, out=d1);
    And(a=ch4, b=cha, out=d2);
    And(a=d1, b=d2, out=d3);
    And(a=d3, b=chf, out=d);

    And(a=ch3, b=ch4, out=e1);
    And(a=ch5, b=ch9, out=e2);
    And(a=e1, b=e2, out=e3);
    And(a=e3, b=d1, out=e);

    And(a=ch2, b=ch3, out=f1);
    And(a=d1, b=chd, out=f2);
    And(a=f1, b=f2, out=f);

    And(a=ch0, b=chc, out=g1);
    And(a=g1, b=d1, out=g);
}
